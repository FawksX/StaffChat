plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group = 'games.synx'
version = '1.0-SNAPSHOT'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    maven {
        name = 'velocitypowered-repo'
        url = 'https://repo.velocitypowered.com/releases/'
    }
    maven {
        name = 'minecraft-libraries'
        url = 'https://libraries.minecraft.net/'
    }
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/maven'
    }
}

dependencies {
    compileOnly 'com.velocitypowered:velocity-api:1.1.5'
    annotationProcessor 'com.velocitypowered:velocity-api:1.1.5'

    compile 'org.spongepowered:configurate-gson:4.0.0'

    compileOnly(fileTree(dir: "libs"))
}

import org.apache.tools.ant.filters.ReplaceTokens

task processSources(type: Sync) {
    from sourceSets.main.java.srcDirs
    inputs.property 'version', version
    filter ReplaceTokens, tokens: [version: version]
    into "$buildDir/src"
}

compileJava.source = processSources.outputs

shadowJar {

    relocate 'org.spongepowered.configurate', 'games.synx.libs.configurate'
    relocate 'io.leangen.geantyref', 'games.synx.libs.geantyref'
    relocate 'org.checkerframework', 'games.synx.libs.checkerframework'

}

jar.archiveName("StaffChat-${project.version}-BUILD.jar")

shadowJar.archiveName("StaffChat-${project.version}.jar")
tasks.build.dependsOn tasks.shadowJar
